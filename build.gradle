plugins {
	id 'com.gradle.build-scan' version '2.3'
	id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'io.freefair.lombok' version '3.8.0'
	id 'idea'
	id 'java'
	id 'maven'
}

apply plugin: 'io.spring.dependency-management'

group = 'eu.flate.glc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

def webappDir = "$projectDir/src/main/webapp"
def webappOut = "$webappDir/dist"
def webappSrc = "$webappDir/src"
def webappModules = "$webappDir/node_modules"
def webappFiles = files(file("$webappDir/angular.json"),
		file("$webappDir/package.json"),
		file("$webappDir/package-lock.json"),
		file("$webappDir/tsconfig.app.json"),
		file("$webappDir/tsconfig.json"),
		file("$webappDir/tsconfig.spec.json"));

def webappTestFile = file("$webappDir/karma.conf.js")


repositories {
	maven { url 'https://repo.spring.io/milestone' }
	mavenCentral()
}

buildScan {
	termsOfServiceUrl = 'https://gradle.com/terms-of-service'
	termsOfServiceAgree = 'yes'
}

springBoot {
	mainClassName = 'eu.flate.glc.GameLibraryCurator'
}

dependencies {
	implementation 'org.hamcrest:hamcrest-core'
	implementation 'org.projectlombok:lombok:1.18.6'
	implementation 'org.reactivestreams:reactive-streams'
	implementation "io.projectreactor.netty:reactor-netty:0.9.0.M2"
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.core:jackson-annotations'
	implementation 'org.springframework:spring-webflux'
	implementation 'org.springframework:spring-tx'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.postgresql:postgresql'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine'
}


sourceSets {
	main {
		resources {
			srcDirs = ["$webappOut", "$projectDir/src/main/resources"]
		}
	}
}

processResources {
	dependsOn "buildAngular"
	dependsOn "createPom"
}


task buildAngular(type: Exec) {
	// installAngular should be run prior to this task
	dependsOn "installAngular"
	workingDir "$webappDir"
	inputs.dir "$webappSrc"
	inputs.files "$webappFiles"
	outputs.dir "$webappOut"
	// Add task to the standard build group
	group = BasePlugin.BUILD_GROUP
	// ng doesn't exist as a file in windows -> ng.cmd
	if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")) {
		commandLine "ng.cmd", "build"
	} else {
		commandLine "ng", "build"
	}
}

task installAngular(type: Exec) {
	workingDir "$webappDir"
	inputs.files "$webappFiles"
	outputs.dir "$webappModules"
	group = BasePlugin.BUILD_GROUP
	if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")) {
		commandLine "npm.cmd", "install"
	} else {
		commandLine "npm", "install"
	}
}


task createPom {
	//noinspection GrUnresolvedAccess
	pom {
		//noinspection GroovyAssignabilityCheck
		project {
			//noinspection GrUnresolvedAccess
			groupId rootProject.group.toString()
			//noinspection GrUnresolvedAccess
			artifactId rootProject.name
			version rootProject.version.toString()

			//noinspection GrUnresolvedAccess
			inceptionYear '2019'
		}
	}.writeTo("pom.xml")
}

test {
	task ciTest {
		dependsOn "karmaTestAngular"
	}

	task ciBuildAngular(type: Exec) {
		dependsOn "ciInstallAngular"
		workingDir "$webappDir"
		inputs.dir "$webappSrc"
		inputs.files "$webappFiles"
		outputs.dir "$webappOut"
		group = BasePlugin.BUILD_GROUP
		if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")) {
			commandLine "ng.cmd", "build", "--prod"
		} else {
			commandLine "ng", "build", "--prod"
		}
	}

	task ciInstallAngular(type: Exec) {
		workingDir "$webappDir"
		inputs.files "$webappFiles"
		outputs.dir "$webappModules"
		group = BasePlugin.BUILD_GROUP
		if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")) {
			commandLine "npm.cmd", "ci"
		} else {
			commandLine "npm", "ci"
		}
	}

	task karmaTestAngular(type: Exec) {
		dependsOn "ciBuildAngular"
		workingDir "$webappDir"
		inputs.dir "$webappSrc"
		inputs.file "$webappTestFile"
		outputs.dir "$webappOut"
		group = BasePlugin.BUILD_GROUP
		if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")) {
			commandLine "ng.cmd", "test", "--progress=false", "--watch=false"
		} else {
			commandLine "ng", "test", "--progress=false", "--watch=false"
		}
	}

}